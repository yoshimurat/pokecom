REM LANDING.BAS FOR PC-G850
REM  OBJECTIVE   :うまく平らな地形に着地してください。
REM  HOW TO PLAY :A:上昇  左右キー:左右に加速
REM  DESCRIPTION :画面色が反転したままの場合は一度電源を入れなおして下さい
REM　             同時押し判定はしていません。

10 RANDOMIZE:CLEAR
20 R=1,S=0,MX=110,MY=47
30 *INIT
40 CLS:VX=0,VY=0,F=0,VY=-.5,X=10,Y=35,NY=35,NX=10,F=0,E=0
50 LOCATE 19,0:PRINT "SCORE"
60 LOCATE 19,2:PRINT "FUEL"
70 LOCATE 19,4:PRINT "R="
80 LINE(0,0)-(MX,MY),B
90 IF R=1 GOTO *R1
100 IF R=2 GOTO *R2
110 IF R=3 GOTO *R3
120 IF R=4 GOTO *R4 ELSE GOTO *R5
130 *TICK
140 Z$=INKEY$
150 IF Z$=CHR$(14) AND VX<2 LET VX=VX+.018
160 IF Z$=CHR$(15) AND VX>-2 LET VX=VX-.018
170 IF Z$="A" AND VY>-1 AND F=0 IF E>0 LET VY=VY-.3:F=1
180 IF Z$="A" AND E>0 LET E=E-1
190 IF Z$="" LET F=0
200 VY=VY+.02:NX=X+VX:NY=Y+VY
210 IF POINT(NX,NY) GOTO *CHK
220 *MOVE
230 PRESET(X,Y)
240 IF NOT (INT(NX) = INT(X)) LET S=S+1
250 X=NX:Y=NY:PSET(NX,NY)
260 LOCATE 20,1:PRINT S
270 LOCATE 20,3:PRINT E
280 LOCATE 21,4:PRINT R
290 GOTO *TICK
300 *CHK
310 B=INT(NX):C=INT(NY)
320 IF C=INT(Y) AND B=INT(X) GOTO *MOVE
330 IF ABS(VY)+ABS(VX)>.4 GOTO *EXP
340 IF VY>0 AND B>0 AND F<MX AND C>0 AND C<MY GOTO *LAND
350 *EXP
360 FOR I=0 TO 40
370 LINE(NX,NY)-(RND(MX),RND(MY))
380 NEXT
390 OUT &H40,&HA7
400 LOCATE 1,5:PRINT "AMAI...":CALL 48381:OUT &H40,&HA6:R=1:S=0:GOTO *INIT
410 *LAND
420 LOCATE 5,5:PRINT "GOOD...":CALL 48381:R=R+1:S=S+E:GOTO *INIT
430 *R1
440 E=50:LOCATE 10,4:PRINT "w---w":GOTO *TICK
450 *R2
460 E=50:LOCATE 0,2:PRINT "**********":LOCATE 15,2:PRINT "--":GOTO *TICK
470 *R3
480 E=30:LOCATE 1,2:PRINT "X XUUUUX":GOTO *TICK
490 *R4
500 E=100:LOCATE 0,1:PRINT "==":LOCATE 0,2:PRINT "WWWIIIWWWWWWWWW":GOTO *TICK
510 *R5
520 E=100:Q=RND 7:Q1=RND 2
530 LOCATE Q,Q1:PRINT "WWWWWWWW WWWW":LOCATE Q,Q1+1:PRINT "WW-wwWWWWWWWW":LOCATE Q,Q1+2:PRINT "WWIWWWWWWWWWW":GOTO *TICK
