REM GRAVITY.BAS FOR PC-G850
REM  OBJECTIVE   :黒いマルまでジャンプしてください
REM  HOW TO PLAY : 1:横軸の速度決定 2:縦軸の速度決定

10 CLEAR:RANDOMIZE
20 R=1,S=0,MX=133,MY=40,PX=0,PY=0
30 DIM B$(6):DIM GX(6):DIM GY(6)
40 *LD1
50 FOR I=1 TO 6:READ GX(I):NEXT
60 FOR I=1 TO 6:READ GY(I):NEXT
70 *INIT
80 CLS:F=0,M=0,VX=0,VY=0,A=3
90 IF R=1 GOSUB *R1
100 IF R=2 GOSUB *R2
110 IF R=3 GOSUB *R3
120 IF R=4 GOSUB *R4
130 IF R>4 GOSUB *R5
140 LOCATE 14,5:PRINT S:LOCATE 19,5:PRINT T:LOCATE 22,0:PRINT R
150 GOSUB *LD2
160 CIRCLE (GX(R)*8+9,GY(R)*8+4),3,,,,,5:PSET(X,Y)
170 *TI
180 IF M=1 GOTO *M1
190 IF M=2 GOTO *M2
200 IF PX>=0 LET PX=PX+2:IF PX>40 LET PX=PX-40:PX=-PX:LINE(4,43)-(84,43),R
210 IF PX<0 LET PX=PX-2:IF PX<-40 LET PX=PX+40:PX=-PX:LINE(4,43)-(84,43),R
220 LINE(44,43)-(44+PX,43)
230 IF INKEY$="1" LET VX=PX:M=1
240 GOTO *CMN
250 *M1
260 PY=PY+2:IF PY>30 LET PY=PY-30:LINE(2,2)-(2,42-PY)
270 IF INKEY$="2" LET VY=PY:M=2:T=T-50
280 GOTO *CMN
290 *M2
300 VY=VY-G:NX=X+VX*.05:NY=Y-VY*.075
310 IF POINT(NX,NY) GOSUB *CHK
320 IF NOT(INT(NX)=INT(X)) LET S=S+1
330 IF NOT(INT(NY)=INT(Y)) LET S=S+2
340 LINE(NX,NY)-(X,Y):X=NX:Y=NY
350 *CMN
360 T=T-1:LOCATE 14,5:PRINT S:LOCATE 19,5:PRINT T:LOCATE 22,0:PRINT R
370 IF T=0 GOTO *EXP ELSE GOTO *TI
380 *CHK
390 IF NX>GX(R)*8+5 AND NX<GX(R)*8+13 AND NY>GY(R)*8 AND NY<GY(R)*8+8 GOTO *LAND
400 IF NX<4 OR NX>MX OR NY<0 OR NY>MY GOTO *EXP
410 SX=INT((NX-5)/8):SY=INT(NY/8):RX=INT((X-5)/8):RY=INT(Y/8)
420 IF MID$(B$(SY+1),SX+1,1)="0" RETURN
430 FX=0
440 IF RX>SX LET NX=2*((SX+1)*8+5)-NX
450 IF RX<SX LET NX=2+(SX*8+5)-NX
460 IF RY>SY LET NY=2*8*(SY+1)-NY:VY=-VY:FX=1
470 IF RY<SY LET NY=SY*8-1:VX=0:VY=:M=0
480 IF FX=0 AND NOT(RX=SX) LET VX=-VX
490 RETURN
500 *EXP
510 FOR I=0 TO 40:LINE(NX,NY)-(RND(MX),RND(MY)),X:NEXT
520 OUT &H40,&HA7
530 LOCATE 1,4:PRINT "AMAI..":CALL 48381:OUT &H40,&HA6:R=1,S=0:RESTORE:GOTO *LD1
540 *LAND
550 LOCATE 5,4:PRINT " WELL DONE..":CALL 48381:R=R+1,S=S+T,D=0:GOTO *INIT
560 *LD2
570 FOR I=1 TO 5:READ B0:GOSUB *D2BIN
580 FOR J=1 TO 16
590 IF MID$(B$(I),J,1)="1" LINE((J-1)*8+5,(I-1)*8)-(J*8+5,I*8),BF
600 NEXT:NEXT:RETURN
610 *D2BIN
620 B$(I)=""
630 *D2B
640 B1=INT(B0/2):B2=B0-2*B1
650 B$(I)=CHR$(B2+48)+B$(I)
660 IF B1>0 LET B0=B1:GOTO *D2B
670 WHILE LEN(B$(I))<16:B$(I)="0"+B$(I):WEND
680 RETURN
690 *R1
700 G=1,T=999,X=36,Y=MY-9:RETURN
710 *R2
720 G=1,T=999,X=MX-12,Y=MY-9:RETURN
730 *R3
740 G=1,T=999,X=9,Y=MY-9:RETURN
750 *R4
760 G=1,T=999,X=20,Y=MY-9:RETURN
770 *R5
780 G=1,T=1500,X=9,Y=MY-9:RETURN
790 DATA 8,0,13,0,10,10
800 DATA 3,2,2,0,1,1
810 DATA 65535,32769,32769,32785,65535
820 DATA 256,0,0,256,65535
830 DATA 0,128,896,0,65535
840 DATA 0,0,33664,32768,63551
850 DATA 65535,8257,46717,34305,61439
860 DATA 0,0,384,32770,6144
